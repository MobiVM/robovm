plugins {
    id 'java'
    id "org.jetbrains.intellij" version "1.5.2"
}

ext {
    roboVMVersion = '2.3.18'
}

group 'com.mobidevelop.robovm'
version "${roboVMVersion}"

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

configurations {
    robovm_dist
}

dependencies {
    implementation ("com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}") {
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-rt'
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-objc'
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-cocoatouch'
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-cacerts-full'
    }
    implementation ("com.mobidevelop.robovm:robovm-ibxcode:${roboVMVersion}") {
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-compiler'
    }
    implementation ("com.mobidevelop.robovm:robovm-templater:${roboVMVersion}") {
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-templates-ios-single-view-no-ib'
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-templates-ios-framework'
        exclude group: 'com.mobidevelop.robovm', module: 'robovm-templates-console'
    }
    robovm_dist "com.mobidevelop.robovm:robovm-dist:${roboVMVersion}:nocompiler@tar.gz"
}

intellij {
    version = '2021.1'   // we are compiles against this API version
    plugins = ['java', 'maven', 'gradle']
    updateSinceUntilBuild = false
}

task copyRoboVmDist(type: Copy) {
    from configurations.robovm_dist
    into "src/main/resources/"
    rename "robovm-dist-${roboVMVersion}-nocompiler.tar.gz", "robovm-dist"
}


sourceCompatibility = 1.8
targetCompatibility = 1.8
compileJava {
    dependsOn copyRoboVmDist
}
