buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.7.1'
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'maven'
apply plugin: 'nexus'
apply plugin: 'eclipse'
apply plugin: 'com.github.johnrengelman.shadow'

group = 'com.mobidevelop.robovm'
archivesBaseName = 'robovm-gradle-plugin'
version = '2.3.6-SNAPSHOT'

sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

ext {
                                                    roboVMVersion = '2.3.6-SNAPSHOT'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
}

dependencies {
    compileOnly gradleApi()
    compileOnly localGroovy()
    compile "org.apache.commons:commons-compress:1.5"
    compile "com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}"
    compile "com.mobidevelop.robovm:robovm-debugger:${roboVMVersion}"
    compile 'org.sonatype.aether:aether:1.13.1'
    compile 'org.sonatype.aether:aether-connector-wagon:1.13.1'
    compile 'org.apache.maven:maven-aether-provider:3.0.4'
    compile 'org.apache.maven.wagon:wagon-provider-api:2.4'
    compile 'org.apache.maven.wagon:wagon-http:2.4'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

modifyPom {
    project {
        name 'RoboVM Gradle Plugin'
        packaging 'jar'
        description 'The RoboVM Gradle Plugin provides a way to build RoboVM apps using Gradle.'
        url 'https://github.com/mobidevelop/robovm/'
        inceptionYear '2013'

        scm {
            url 'https://github.com/mobidevelop/robovm.git'
            connection 'scm:git:git://github.com/mobidevelop/robovm.git'
            developerConnection 'scm:git:git@github.com:mobidevelop/robovm.git'
        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }

        developers {
            developer {
                id 'jtakakura'
                name 'Junji Takakura'
            }
            developer {
                id 'ntherning'
                name 'Niklas Therning'
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.13'
}

// apply from: 'local.gradle'

jar {
    enabled = false
}

shadowJar {
    classifier = ''
    dependencies {
        exclude(dependency("com.mobidevelop.robovm:robovm-dist-compiler:${roboVMVersion}"));
    }
    relocate 'org.apache.http', 'com.mobidevelop.robovm.org.apache.http'
    relocate 'org.apache.commons.io', 'com.mobidevelop.robovm.org.apache.commons.io'
    relocate 'org.objectweb.asm', 'com.mobidevelop.robovm.asm'
}

assemble.dependsOn('shadowJar')
install.dependsOn('shadowJar')
